#  vim: set ts=4 sw=4 tw=80 ft=zsh et :
# Alias git to g

assumed_files_info() {
    local GIT_ASSUMED_FILES_COUNT=$(git ls-files -v | grep '^h' | wc -l)
    # show a little prompt at the end if we have files assumed unchanged
    if [[ ${GIT_ASSUMED_FILES_COUNT} -gt 0 ]]; then
        e_info "${GIT_ASSUMED_FILES_COUNT}"
    fi
}

last_commit_info() {
    local GIT_INFO_MESSAGE GIT_INFO_HASH GIT_INFO_TIME COLS
    COLS=$(tput cols)
    if [[ $COLS -gt 80 ]]; then
        COLS=72
    else
        COLS=$(($COLS-8))
    fi
    GIT_LAST_COMMIT_INFO=$(\git log --color=always -n 1 --pretty=format:"%C(yellow)%h%C(reset)|%C(green)%<($COLS,trunc)%s%C(reset)|%C(cyan)%an - %ad%C(reset) " --date=relative 2>/dev/null)

    GIT_INFO_HASH=$(echo $GIT_LAST_COMMIT_INFO | cut -d '|' -f1)
    GIT_INFO_MESSAGE=$(echo $GIT_LAST_COMMIT_INFO | cut -d '|' -f2)
    GIT_INFO_TIME=$(echo $GIT_LAST_COMMIT_INFO | cut -d '|' -f3)
    [[ -n $GIT_INFO_MESSAGE ]] && \
        e_header "Last Commit Info ${GIT_INFO_HASH}" "${GIT_INFO_MESSAGE}" "${GIT_INFO_TIME}"
}


if [[ $# -eq 0 ]]; then
    last_commit_info
    if (( $+commands[git-st] )); then
        $commands[git-st] 2>/dev/null || (e_header "Not in a git repository" && l)
    else
        ${aliases[git]:-git} status --short --branch 2>/dev/null || e_header "Not in a git repository"
    fi
    assumed_files_info
else
    ${aliases[git]:-git} "$@"
fi
